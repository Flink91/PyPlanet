/* ----------------- Checkbox ------------------ */

Void uiRenderCheckbox(CMlFrame frame) {
    declare uiControl = frame.Controls[2];
	if (frame.DataAttributeGet("checked") == "1") {
       AnimMgr.Add(uiControl, "<frame scale=\"1.\" />", 250, CAnimManager::EAnimManagerEasing::BackOut);
    } else {
       AnimMgr.Add(uiControl, "<frame scale=\"0.\" />", 100, CAnimManager::EAnimManagerEasing::BackIn);
	  }
      declare CMlEntry entry = (frame.Controls[0] as CMlEntry);
	    entry.Value = frame.DataAttributeGet("checked") ;
}

Void uiToggleCheckbox(CMlFrame frame) {
	if  (frame.DataAttributeGet("checked") == "1") {
		frame.DataAttributeSet("checked", "0");
	} else {
		frame.DataAttributeSet("checked", "1");
	}
	uiRenderCheckbox(frame);
}

Void uiSetCheckbox(CMlFrame frame, Boolean status) {
	if (status) {
	  frame.DataAttributeSet("checked", "1");
	} else {
		frame.DataAttributeSet("checked", "0");
	}
	uiRenderCheckbox(frame);
}

***OnInit***
***
Page.GetClassChildren ("uiContainer", Page.MainFrame, True);
foreach (frame in Page.GetClassChildren_Result) {
    if (frame.HasClass("uiCheckbox")) {
        uiRenderCheckbox((frame as CMlFrame));
    }
}
***


***OnMouseClick***
***
if (Event.Control.HasClass("uiCheckboxElement") ) {
    if (Event.Control.Parent.HasClass("uiCheckbox")) {
        uiToggleCheckbox(Event.Control.Parent);
    }
}
***

/* ------------------ end Checkbox ------------------- */

/* ------------------- Button ------------------------- */
Void TriggerButtonClick(CMlControl Control) {
    declare Parent = Control.Parent;
     if (Parent.HasClass("uiButton")) {
        Parent.RelativeScale = 0.75;
        AnimMgr.Add(Parent, "<elem scale=\"1.\" />", 200, CAnimManager::EAnimManagerEasing::QuadIn);
        TriggerPageAction(Parent.DataAttributeGet("action"));
     }
}

Void TriggerButtonClick(Text ControlId) {
    declare Control <=> Page.GetFirstChild(ControlId);
    TriggerButtonClick(Control);
}

***OnMouseClick***
***
  if (Event.Control.HasClass("uiButtonElement") ) {
       TriggerButtonClick(Event.Control);
  }
***

***OnMouseOver***
***
if (Event.Control.Parent.HasClass("uiButton")) {
    (Event.Control.Parent as CMlFrame).RelativeScale=1.;
}
***

***OnMouseOut***
***
if (Event.Control.Parent.HasClass("uiButton")) {
    (Event.Control.Parent as CMlFrame).RelativeScale=1.;
}
***

/* ------------------- end Button ------------------------- */



/*  ------------------- ConfirmButton ------------------------- */

***OnInit***
***
    declare Integer[Ident] pendingConfirms for Page = Integer[Ident];
    declare Text[Ident] pendingConfirmIds for Page = Text[Ident];
    declare CMlLabel[Ident] pendingConfirmControls for Page = CMlLabel[Ident];

    pendingConfirms.clear();
    pendingConfirmIds.clear();
    pendingConfirmControls.clear();
***

***Loop***
***
foreach (Id => Time in pendingConfirms) {
    if (Now > Time + (3 * 1000) ) {
       if (pendingConfirmIds.existskey(Id))  {
            pendingConfirmControls[Id].Value = pendingConfirmIds[Id];
            pendingConfirmIds.removekey(Id);
            pendingConfirms.removekey(Id);
            pendingConfirmControls.removekey(Id);
        }
    }
}
***

***OnMouseClick***
***
if (Event.Control.HasClass("uiConfirmButtonElement") ) {
    TriggerConfirmButtonClick((Event.Control as CMlLabel));
}
***


Void TriggerConfirmButtonClick(CMlLabel Control) {
       declare Integer[Ident] pendingConfirms for Page = Integer[Ident];
       declare Text[Ident] pendingConfirmIds for Page = Text[Ident];
       declare CMlLabel[Ident] pendingConfirmControls for Page = CMlLabel[Ident];

       if (Control.Parent.HasClass("uiButton")) {
              if (pendingConfirmIds.existskey(Control.Id) == False) {
                    pendingConfirmIds[Control.Id] = Control.Value;
                    pendingConfirmControls[Control.Id] = Control;
                    pendingConfirms[Control.Id] = Now;
                    Control.Value = "Confirm ?";
                    Control.Parent.RelativeScale = 0.75;
                    AnimMgr.Add(Control.Parent, "<elem scale=\"1.\" />", 200, CAnimManager::EAnimManagerEasing::QuadIn);
              } else {
                 Control.Value = pendingConfirmIds[Control.Id];
                 pendingConfirmIds.removekey(Control.Id);
                 pendingConfirms.removekey(Control.Id);
                 pendingConfirmControls.removekey(Control.Id);
                 Control.Parent.RelativeScale = 0.75;
                 AnimMgr.Add(Control.Parent, "<elem scale=\"1.\" />", 200, CAnimManager::EAnimManagerEasing::QuadIn);
                 TriggerPageAction(Control.Parent.DataAttributeGet("action"));
             }
       }
}


Void TriggerConfirmButtonClick(Text ControlId) {
    declare CMlLabel Control = (Page.GetFirstChild(ControlId) as CMlLabel);
    TriggerConfirmButtonClick(Control);
}

/*  ------------------- end ConfirmButton ------------------------- */

/*  ------------------- Dropdown ------------------------- */

Void uiRenderDropdown(CMlFrame frame) {
        declare selected = TextLib::ToInteger(frame.DataAttributeGet("selected"));
        declare index = 0;

        declare options = (frame.Controls[3] as CMlFrame);

            if (frame.DataAttributeGet("open") == "1") {
                frame.Controls[3].Show();
            } else {
                 frame.Controls[3].Hide();
            }

            foreach (option in options.Controls) {
                if (selected == index) {
                    (frame.Controls[1] as CMlLabel).Value = (option as CMlLabel).Value;
                    (frame.Controls[2] as CMlEntry).Value = option.DataAttributeGet("value");
                }
            index+= 1;
        }
    }

    Void uiToggleDropdown (CMlFrame frame) {
        if (frame.DataAttributeGet("open") == "1") {
            frame.DataAttributeSet("open","0");
        } else {
            frame.DataAttributeSet("open","1");
        }
         uiRenderDropdown(frame);
    }

    Void uiSelectDropdown (CMlLabel label) {
        declare uiDropdown = label.Parent.Parent;
        uiDropdown.DataAttributeSet("selected", label.DataAttributeGet("index"));
        uiDropdown.DataAttributeSet("value", label.DataAttributeGet("value"));
        uiRenderDropdown(uiDropdown);
        uiToggleDropdown(uiDropdown);
        +++onSelectDropdown+++
    }

***OnInit***
***
Page.GetClassChildren("uiContainer", Page.MainFrame, True);
foreach (frame in Page.GetClassChildren_Result) {
    if (frame.HasClass("uiDropdown")) {
        uiRenderDropdown((frame as CMlFrame));
    }
}
***

***OnMouseClick***
***
if (Event.Control.HasClass("uiSelectElement")) {

        if (Event.Control.Parent.HasClass("uiDropdown")) {
            uiToggleDropdown(Event.Control.Parent);
        }

        if (Event.Control.Parent.HasClass("uiDropdownSelect")) {
            uiSelectDropdown((Event.Control as CMlLabel));
        }
}
***


/*  ------------------- end Dropdown ------------------------- */

/*  ------------------- Scrollable area ------------------------- */

***OnInit***
***
declare CMlFrame py_scroll_frame = Null;
declare CMlFrame py_scroll_content = Null;
declare Vec2 py_scroll_content_size = <0.,0.>; 
declare Boolean py_scroll_activeY = False;
declare Boolean py_scroll_activeX = False;
declare Vec2 py_scroll_pos = <0.,0.>;
***

***OnMouseClick***
***
if (Event.Control != Null && Event.Control.HasClass("uiScrollbar") )  {
	if (Event.Control.DataAttributeGet("axis") == "X") {
		py_scroll_activeX = True;
	} else {
		py_scroll_activeY = True;																
	}
	py_scroll_frame = Event.Control.Parent;
	py_scroll_pos = <MouseX, MouseY> - Event.Control.RelativePosition_V3 ;
	py_scroll_content = (py_scroll_frame.Parent.Controls[1] as CMlFrame); // gets the bounding frame				
	py_scroll_content_size = py_scroll_content.Controls[0].Size;             
}
***

***Loop***
***
if (py_scroll_activeY) {		
			declare Real pos = (MouseY - py_scroll_pos.Y) ;
			declare Real upperLimit = py_scroll_frame.RelativePosition_V3.Y - py_scroll_frame.RelativePosition_V3.Y - 5.;
			declare Real lowerLimit =  upperLimit - py_scroll_frame.Controls[3].Size.Y + py_scroll_frame.Controls[0].Size.Y + 10.;
			
			if (pos > upperLimit) {
				pos = upperLimit;
			}
													
			if (pos < lowerLimit)  {  
				pos = lowerLimit;
			}
			
		declare Real start = (upperLimit - pos);
		declare Real diff = MathLib::Abs(lowerLimit - upperLimit);
						
		py_scroll_frame.Controls[0].RelativePosition_V3.Y = pos; // update scrollbar position												
		py_scroll_content.Controls[0].RelativePosition_V3.Y = (start / diff) * (py_scroll_content_size.Y - py_scroll_frame.Parent.Controls[0].Size.Y + 40.);  //  gets the content frame
}

if (py_scroll_activeX) {		
			declare Real pos = ( MouseX - py_scroll_pos.X);
			declare Real leftLimit = 5.;
			declare Real rightLimit =  leftLimit + py_scroll_frame.Controls[3].Size.X - py_scroll_frame.Controls[0].Size.X -10.;
			
			if (pos < leftLimit) {
				pos = leftLimit;
			}
													
			if (pos > rightLimit)  {  
				pos = rightLimit;
			}
			
		declare Real start =  (leftLimit - pos);
		declare Real diff = MathLib::Abs(leftLimit + rightLimit);
						
		py_scroll_frame.Controls[0].RelativePosition_V3.X = pos; // update scrollbar position												
		py_scroll_content.Controls[0].RelativePosition_V3.X = (start / diff) * (py_scroll_content_size.X);  //  gets the content frame
}



if (MouseLeftButton == False)  {
	py_scroll_activeX = False;
	py_scroll_activeY = False;
}

***

/*  ------------------- end Scrollable area ------------------------- */



